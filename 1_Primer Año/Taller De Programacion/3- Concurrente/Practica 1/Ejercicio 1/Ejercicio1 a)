programa Programa1
procesos
  proceso juntarFlores(ES floresRecolectadas:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      floresRecolectadas := floresRecolectadas+1
  fin
  
  proceso evaluarEsquina(ES floresRecolectadas: numero; ES esquinasSinFlores: numero)
  comenzar
    si(HayFlorEnLaEsquina)
      juntarFlores(floresRecolectadas)
    sino
      esquinasSinFlores := esquinasSinFlores+1
  fin
  proceso depositarTodo(E cant:numero)
  comenzar
    repetir cant
      depositarFlor
  fin
  
  proceso recorrerAvenida
  variables
    esquinasSinFlores: numero
    floresRecolectadas: numero
  comenzar
    esquinasSinFlores := 0
    floresRecolectadas := 0
    evaluarEsquina(floresRecolectadas,  esquinasSinFlores)
    repetir 99 
      mover
      evaluarEsquina(floresRecolectadas,  esquinasSinFlores)   
    depositarTodo(floresRecolectadas)
    Informar("Cant_Flores", floresRecolectadas)
    Informar("Esquinas_Vacias", esquinasSinFlores)
  fin
  
  
areas
  ciudad: AreaC (1,1,100,100)

robots
  robot robot1
  
  comenzar
    recorrerAvenida
    repetir 2
      Pos(PosAv+2, 1)
      recorrerAvenida
  fin
  
  
variables
  R_info: robot1
comenzar
  AsignarArea(R_info, ciudad)
  Iniciar(R_info, 1,1)
fin
{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
  fin
areas
  cuadrante: AreaPC (40,40,60,60)
  deposito: AreaPC (10,10,10,10)
  area1: AreaP(1,1,1,1)
  area2: AreaP(2,1,2,1)
  area3: AreaP(3,1,3,1)
  areaF: AreaP(4,1,4,1)
robots
  robot florero
  variables
    id, avInicio, caInicio, av, ca:numero
    ok:boolean
  comenzar
    RecibirMensaje(id, RF)
    avInicio:=PosAv
    caInicio:=PosCa
    
    RecibirMensaje(ok, RF)
    mientras(ok)
      RecibirMensaje(av, RF)
      RecibirMensaje(ca, RF)
      
      BloquearEsquina(av,ca)
      Pos(av,ca)
      si(HayFlorEnLaEsquina)
        juntarFlores
      Pos(avInicio, caInicio)
      LiberarEsquina(av, ca)
      ok:=V
      
      RecibirMensaje(ok,RF)
      
    {dejar las flores en la esquina 10,10}  
    BloquearEsquina(10,10)
    Pos(10,10)
    mientras(HayFlorEnLaBolsa)
      depositarFlor
    Pos(avInicio, caInicio)
    LiberarEsquina(10,10)     
    
    EnviarMensaje(ok, RF) 
  fin
  
  robot fiscalizador
  variables
    id, av, ca, flores, avInicial, caInicial:numero
    ok:boolean
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    flores:=0
    avInicial:=PosAv
    caInicial:=PosCa
        
    repetir 8
      Random(id,1,3)
      Random(av, 40,60)
      Random(ca, 40,60)
      si(id=1)
        EnviarMensaje(V, r1)
        EnviarMensaje(av, r1)
        EnviarMensaje(ca, r1)
      sino  
        si(id=2)
          EnviarMensaje(V, r2)
          EnviarMensaje(av, r2)
          EnviarMensaje(ca, r2)
        sino
          EnviarMensaje(V, r3)
          EnviarMensaje(av, r3)
          EnviarMensaje(ca, r3)
    
    EnviarMensaje(F , r1)
    EnviarMensaje(F , r2)
    EnviarMensaje(F , r3)
    
    repetir 3
      RecibirMensaje(ok, *)
    
    BloquearEsquina(10,10)
    Pos(10,10)
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores:=flores+1
    Pos(avInicial, caInicial)
    LiberarEsquina(10,10)
    Informar(flores)
  fin
  
variables
  r1,r2,r3: florero
  RF: fiscalizador
comenzar
  AsignarArea(RF, areaF)
  AsignarArea(RF, deposito)
  
  AsignarArea(r1, area1)
  AsignarArea(r1, cuadrante)
  AsignarArea(r1, deposito)
  
  AsignarArea(r2, area2)
  AsignarArea(r2, cuadrante)
  AsignarArea(r2, deposito)
  
  AsignarArea(r3, area3)
  AsignarArea(r3, cuadrante)
  AsignarArea(r3, deposito)
  
  Iniciar(r1, 1,1)
  Iniciar(r2, 2,1)
  Iniciar(r3, 3,1)
  Iniciar(RF, 4,1)
fin